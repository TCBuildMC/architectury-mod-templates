import net.fabricmc.loom.task.RemapJarTask

plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        setEnableTransitiveAccessWideners true
        setEnableModProvidedJavadoc true
        silentMojangMappingsLicense()
    }

    repositories {
        maven {
            url = "https://maven.quiltmc.org/repository/release/"
        }
        maven {
            url = "https://maven.parchmentmc.org/"
        }
        maven {
            url = "https://maven.architectury.dev/"
        }
        maven {
            url = "https://maven.shedaniel.me/"
        }
        maven {
            url = "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
        maven {
            url = "https://jitpack.io/"
            content {
                includeGroupAndSubgroups "com.github"
            }
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            it.mappings("org.quiltmc:quilt-mappings:${rootProject.quilt_mappings_version}:intermediary-v2")
            it.parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}@zip")
            it.officialMojangMappings { nameSyntheticMembers = false }
        }
        annotationProcessor(compileOnly("org.projectlombok:lombok:${rootProject.lombok_version}"))
    }

    tasks.register('remapSlimJar', RemapJarTask) {
        dependsOn jar
        inputFile.set jar.archiveFile
        addNestedDependencies = false
        archiveClassifier.set "slim"
    }

    tasks.remapJar.dependsOn remapSlimJar
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name + "-" + project.name + "+" + rootProject.minecraft_version
    }

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    compileJava.dependsOn clean

    java {
        withSourcesJar()
    }

    publishing {
        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            mavenLocal()
        }
    }
}

forgix {
    group = rootProject.maven_group // (Required Value)
    mergedJarName = rootProject.archives_base_name + "+" + rootProject.minecraft_version + "-" + rootProject.mod_version + ".jar" // (Required Value)
    outputDir = "build/libs/merged"

    forge {
        projectName = "forge"
        jarLocation = "build/libs/" + rootProject.archives_base_name + "-forge+" + rootProject.minecraft_version + "-" + rootProject.mod_version + ".jar"

//        additionalRelocate "org.my.lib" "forge.org.my.lib"
//        additionalRelocate "org.my.lib.another" "forge.org.my.lib.another"

        mixin "examplemod.mixins.json"
    }

//    neoforge {
//        projectName = "neoforge"
//        jarLocation = "build/libs/" + rootProject.archives_base_name + "-neoforge+" + rootProject.minecraft_version + "-" + rootProject.mod_version + ".jar"
//
////        additionalRelocate "org.my.lib" "neoforge.org.my.lib"
////        additionalRelocate "org.my.lib.another" "neoforge.org.my.lib.another"
//
//        mixin "examplemod.mixins.json"
//    }

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/" + rootProject.archives_base_name + "-fabric+" + rootProject.minecraft_version + "-" + rootProject.mod_version + ".jar"

//        additionalRelocate "org.my.lib" "fabric.org.my.lib"
//        additionalRelocate "org.my.lib.another" "fabric.org.my.lib.another"
    }

//    removeDuplicate "org.example.mod.core"
}
